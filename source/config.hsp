/* ソフト設定関係 */
#module Config
	// 設定読み込み用ユーティリティ
	// (返り値がTRUEだとエラーが発生している)
	#defcfunc jsongetint var variable_, str name_, int ptr_
		jsongeti@ temp, name_, ptr_
		if(stat == 1) :return TRUE
		variable_ = temp
	return FALSE
	#defcfunc jsongetstr var variable_, str name_, int ptr_
		sdim temp, 1024
		jsongets@ temp, name_, ptr_
		if(stat == 1) :return TRUE
		variable_ = temp
	return FALSE
	// 設定を読み込んで初期化する
	#deffunc initConfig
		// 設定を初期化
		// 保持する時間表記
		ddim exp_timer, FLEET_SIZE
		dim window_pos, WINDOW_COUNT, 3	//window_pos(ウィンドウ数, [x,y,フラグ])
		dim window_show_flg, WINDOW_COUNT	//window_show_flg(インデックス0は不使用)
		// 設定を読み込み
		if(loadConfig()){
			dialog "セーブデータを読み込めませんでした.\nデフォルトの設定で起動します.", 1, SOFT_NAME
			twitter_flg = FALSE	//Twitter投稿用にスクショを加工するならTRUE
			getpos_flg = FALSE	//座標を取得できていればTRUE
			bg_color = $000000	//背景色設定
			for fi, 0, FLEET_SIZE
				exp_timer(fi) = -1.0
			next
			for wi, 0, WINDOW_COUNT
				window_pos(wi, 0) = -1
				window_pos(wi, 1) = -1
				window_pos(wi, 2) = FALSE
				window_show_flg(wi) = FALSE
			next
		}
		// フォルダの存在をチェックして生成
		dirlist x, pic_path@, 5
		if(stat == 0) :mkdir pic_path@
		dirlist x, save_path@, 5
		if(stat == 0) :mkdir save_path@
	return
	// JSONから設定を読み込む
	// 返り値がTRUEだと読み込み時にエラーが発生していることを示す
	#defcfunc loadConfig
		exist save_path@ + "\\config.json"
		if(strsize == -1) :return TRUE
		// ファイルを開く
		notesel json_str
		noteload save_path@ + "\\config.json"
		jsonopen@ json_ptr, json_str
		// version
		version_str = ""
		if(jsongetstr(version_str, "version", json_ptr)) :return TRUE
		strrep version_str, ".", ""
		if(int(version_str) >= 111){
			// window_pos
			for wi, 0, WINDOW_COUNT
				str_window_pos = ""
				if(jsongetstr(str_window_pos, "window_pos_x_" + wi, json_ptr)) :return TRUE
				window_pos(wi, 0) = int(str_window_pos)
				if(jsongetstr(str_window_pos, "window_pos_y_" + wi, json_ptr)) :return TRUE
				window_pos(wi, 1) = int(str_window_pos)
				window_pos(wi, 2) = TRUE
			next
			// window_show_flg
			for wi, 0, WINDOW_COUNT
				if(jsongetint(window_show_flg_, "window_show_flg_" + wi, json_ptr)) :return TRUE
				window_show_flg(wi) = limit(window_show_flg_, FALSE, TRUE)
			next
		}
		// twitter_flg
		if(jsongetint(twitter_flg, "twitter_flg", json_ptr)) :return TRUE
		twitter_flg = limit(twitter_flg, FALSE, TRUE)
		// bg_color
		str_color = ""
		if(jsongetstr(str_color, "bg_color", json_ptr)) :return TRUE
		split str_color, ",", temp
		if(length(temp) < 3) :return TRUE
		rgb_r = limit(int(temp(0)), 0, 255)
		rgb_g = limit(int(temp(1)), 0, 255)
		rgb_b = limit(int(temp(2)), 0, 255)
		bg_color = pack_rgb(rgb_r, rgb_g, rgb_b)
		// exp_timer
		for fi, 0, FLEET_SIZE
			str_exp_timer = ""
			if(jsongetstr(str_exp_timer, "exp_timer_" + fi, json_ptr)) :return TRUE
			exp_timer(fi) = double(str_exp_timer)
		next
		jsonclose@
	return FALSE
	// 設定をJSONに保存する
	#deffunc saveConfig
		sdim json_str, 32768
		json_str = "{\n"
		json_str += "    \"twitter_flg\":" + twitter_flg + ",\n"
		json_str += "    \"bg_color\":\"" + ToStrColor(bg_color) + "\",\n"
		for fi, 0, FLEET_SIZE
			json_str += "    \"exp_timer_" + fi + "\":\"" + exp_timer(fi) + "\",\n"
		next
		for wi, 0, WINDOW_COUNT
			gsel wi
			json_str += "    \"window_pos_x_" + wi + "\":\"" + ginfo_wx1 + "\",\n"
			json_str += "    \"window_pos_y_" + wi + "\":\"" + ginfo_wy1 + "\",\n"
		next
		for wi, 0, WINDOW_COUNT
			json_str += "    \"window_show_flg_" + wi + "\":" + window_show_flg(wi) + ",\n"
		next
		json_str += "    \"version\":\"" + SOFT_VER + "\"\n"
		json_str += "}\n"
		notesel json_str
		notesave save_path@ + "\\config.json"
	return
#global
