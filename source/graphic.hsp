/* 画像処理関係 */
#module Graphic
	#const TEMP_A2D_BUFFER_ID 0
	#const DHASH_SIZE  2	//Difference Hashは64bit＝32bit×2(DHASH_SIZE)で表現
	#const DHASH_HEIGHT 8	//Difference HashはDHASH_WIDTH×DHASH_HEIGHTピクセル
	#const DHASH_WIDTH  9	//に縮小した後に符号化する
	// PNG形式で保存する
	#deffunc pngsave str filename_
		// 転送前の画像についての情報を取得する
		id = ginfo_sel
		wx = ginfo_winx
		wy = ginfo_winy
		// A2Dバッファを作成し、そちらに転送する
		alCreateImage TEMP_A2D_BUFFER_ID, wx, wy
		alCopyScreenToImage id, TEMP_A2D_BUFFER_ID
		// 必要ならば、Twitter用に適宜加工する
		if(twitter_flg@Config){
			// 加工する
			clr = alGetPixel(0, 0)
			clr_r = ARGB_R(clr) :clr_g = ARGB_G(clr) :clr_b = ARGB_B(clr) :clr_a = ARGB_A(clr)
			alSetPixel 0, 0, RGBA(clr_r, clr_g, clr_b, limit(clr_a - 1, 0, 255))
			// 保存する
			alSaveFile pic_path@ + "\\ " + filename_ + "_twi.png", "image/png"
		}else{
			// 保存する
			alSaveFile pic_path@ + "\\ " + filename_+ ".png", "image/png"
		}
	return
	// 画像をカレントカラーで切り抜く
	#deffunc getSquare array input_rect_, array output_rect_
		;カレントカラーを記憶しておく
		cr = ginfo_r
		cg = ginfo_g
		cb = ginfo_b
		;検索範囲を整理する
		x_min = input_rect_(0)
		y_min = input_rect_(1)
		x_max = input_rect_(2)
		y_max = input_rect_(3)
		;検索する(上・左・下・右の順)
		for x, x_min, x_max
			flg = FALSE
			for y, y_min, y_max
				pget x, y
				if((ginfo_r != rr) || (ginfo_g != gg) || (ginfo_b != bb)) :flg = TRUE :_break
			next
			if(flg) :output_rect_(0) = x :_break
		next
		for y, y_min, y_max
			flg = FALSE
			for x, x_min, x_max
				pget x, y
				if((ginfo_r != rr) || (ginfo_g != gg) || (ginfo_b != bb)) :flg = TRUE :_break
			next
			if(flg) :output_rect_(1) = y :_break
		next
		for x, x_max - 1, x_min - 1, -1
			flg = FALSE
			for y, y_min, y_max
				pget x, y
				if((ginfo_r != rr) || (ginfo_g != gg) || (ginfo_b != bb)) :flg = TRUE :_break
			next
			if(flg) :output_rect_(2) = x :_break
		next
		for y, y_max - 1, y_min - 1, -1
			flg = FALSE
			for x, x_min, x_max
				pget x, y
				if((ginfo_r != rr) || (ginfo_g != gg) || (ginfo_b != bb)) :flg = TRUE :_break
			next
			if(flg) :output_rect_(3) = y :_break
		next
	return
	/* 画像をカレントカラーで切り抜く(内部から展開する版)
	 * まず、元のスクリーンに対し、次の3種が存在する。
	 * ・input_rect(外枠)
	 * ・output_rect(抜き出したい内枠)
	 * ・cx, cy(クリック座標)
	 * これに対して、内側からサーチすることで、output_rectを算出する
	 * virtual_flgがTRUEだと、virtual_screen_px@GetPicとvirtual_screen_py@GetPicを
	 * 考慮してズラして算出してくれる(getWindowPositionByClick用)
	 */
	#deffunc getSquareInner array input_rect_, array output_rect_, int cx_, int cy_, int virtual_flg_
		;カレントカラーを記憶しておく
		cr = ginfo_r
		cg = ginfo_g
		cb = ginfo_b
		;検索範囲を整理する
		x_min = input_rect_(0)
		y_min = input_rect_(1)
		x_max = input_rect_(2)
		y_max = input_rect_(3)
		output_rect_(0) = x_min
		output_rect_(1) = y_min
		output_rect_(2) = x_max
		output_rect_(3) = y_max
		;オフセットについて
		if(virtual_flg_){
			offset_x = -virtual_screen_px@GetPic
			offset_y = -virtual_screen_py@GetPic
		}else{
			offset_x = 0
			offset_y = 0
		}
		;検索する(上・左・下・右の順)
		for y, cy_ - 1, y_min - 1, -1
			pget cx_ + offset_x, y + offset_y
			if((ginfo_r == cr) && (ginfo_g == cg) && (ginfo_b == cb)){
				output_rect_(1) = y + 1
				_break
			}
		next
		for x, cx_ - 1, x_min - 1, -1
			pget x + offset_x, cy_ + offset_y
			if((ginfo_r == cr) && (ginfo_g == cg) && (ginfo_b == cb)){
				output_rect_(0) = x + 1
				_break
			}
		next
		for y, cy_ + 1, y_max + 1
			pget cx_ + offset_x, y + offset_y
			if((ginfo_r == cr) && (ginfo_g == cg) && (ginfo_b == cb)){
				output_rect_(3) = y - 1
				_break
			}
		next
		for x, cx_ + 1, x_max + 1
			pget x + offset_x, cy_ + offset_y
			if((ginfo_r == cr) && (ginfo_g == cg) && (ginfo_b == cb)){
				output_rect_(2) = x - 1
				_break
			}
		next
	return
	// rgbで色指定
	#deffunc color_rgb int rgb_
		color (rgb_ >> 16), (rgb_ >> 8) & $FF, rgb_ & $FF
	return
	// r,g,bをrgbに変換
	#defcfunc pack_rgb int r_, int g_, int b_
	return (r_ << 16) | (g_ << 8) | b_
	// rgbを文字に変換
	#defcfunc ToStrColor int rgb_
		rgb_r = (rgb_ >> 16)
		rgb_g = (rgb_ >> 8) & $FF
		rgb_b = rgb_ & $FF
	return strf("%d,%d,%d", rgb_r, rgb_g, rgb_b)
	// DifferenceHashを取得する
	#deffunc getDifferenceHash array ahash_, int buffer_id_, double px_, double py_, double wx_, double wy_
		// ゲーム画面の大きさから、本当のpx・py・wx・wyを取得する
		px = int(px_ * window_wx@GetPic / 100)
		py = int(py_ * window_wy@GetPic / 100)
		wx = int(wx_ * window_wx@GetPic / 100)
		wy = int(wy_ * window_wy@GetPic / 100)
		// トリミング・リサイズ処理を行う
		buffer TEMP_BUFFER_ID, DHASH_WIDTH, DHASH_HEIGHT
		gzoom DHASH_WIDTH, DHASH_HEIGHT, buffer_id_, px, py, wx, wy, 1
		// 画像をモノクロ化する
		ddim mono, DHASH_WIDTH, DHASH_HEIGHT
		for y, 0, DHASH_HEIGHT
			for x, 0, DHASH_WIDTH
				pget x, y
				mono(x, y) = 0.299 * ginfo_r + 0.587 * ginfo_g + 0.114 * ginfo_b
			next
		next
		// 隣接ピクセルとの比較結果を符号化する
		dim ahash_, DHASH_SIZE
		for k, 0, DHASH_SIZE
			for y, k * 4, (k + 1) * 4
				for x, 0, DHASH_WIDTH - 1
					ahash_(k) = ahash_(k) << 1
					if(mono(x, y) > mono(x + 1, y)) :ahash_(k) |= 1
				next
			next
		next
	return
#global
