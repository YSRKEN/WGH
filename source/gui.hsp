/* GUI関係 */
#module GUI
	// ウィンドウID
	#const MAIN_WINDOW_ID 0
	// オブジェクトサイズ
	#const OBJ_X 64
	#const OBJ_Y 24
	#const OBJ_SPACE 10
	#define ctype POS_X(%1) (OBJ_X * (%1) + OBJ_SPACE * ((%1) + 1))
	#define ctype POS_Y(%1) (OBJ_Y * (%1) + OBJ_SPACE * ((%1) + 1))
	#define ctype SIZE_X(%1) (OBJ_X * (%1) + OBJ_SPACE * ((%1) - 1))
	#define ctype SIZE_Y(%1) (OBJ_X * (%1) + OBJ_SPACE * ((%1) - 1))
	// API
	#const WM_NCLBUTTONDOWN $000000A1
	#const HTCAPTION 2
	// GUIを初期化
	#deffunc initGUI
		dim hwnd_window
		// メイン画面
		screen MAIN_WINDOW_ID, POS_X(3), OBJ_SPACE * 3 + OBJ_Y * 2
		hwnd_window(MAIN_WINDOW_ID) = hwnd
		gsel MAIN_WINDOW_ID, 2
		title SOFT_NAME
		pos POS_X(0), POS_Y(0) :objsize SIZE_X(2), OBJ_Y :button gosub "座標取得", *getWPBC :button_getwpbc_id = stat
		pos POS_X(2), POS_Y(0) :objsize OBJ_X, OBJ_Y     :button gosub "ソフト情報", *showInfo
		pos POS_X(0), POS_Y(1) :objsize OBJ_X, OBJ_Y     :button gosub "画像保存", *savePicture
		pos POS_X(1), POS_Y(1) :objsize POS_X(3) - OBJ_X - OBJ_Y - OBJ_SPACE * 4, OBJ_Y    :chkbox "Twitter用に保存", twitter_flg@Config
		pos POS_X(3) - OBJ_Y - OBJ_SPACE, POS_Y(1) :objsize OBJ_Y, OBJ_Y :button gosub "開", *openPictureFloder
	return
*getWPBC
	if(getWindowPositionByClick()){
		gsel MAIN_WINDOW_ID
		objprm button_getwpbc_id, "(" + window_px@GetPic + "," + window_py@GetPic + ") " + window_wx@GetPic + "x" + window_wy@GetPic
	}
	return
*showInfo
	// ソフト情報を表示する処理
	dialog "ソフト名：" + SOFT_NAME + "\nバージョン：" + SOFT_VER + "\n最終更新日：" + SOFT_TIME + "\n作者：YSR"
	return
*savePicture
	// スクショを保存する処理
	saveWindowImage
	return
*openPictureFloder
	// 画像の保存フォルダをエクスプローラで開く処理
	exec "explorer.exe " + pic_path@
	return
*move
	// ウィンドウをドラッグ可能にする処理
	if(wparam != 1) :return
	sendmsg hwnd_window(ginfo_act), WM_NCLBUTTONDOWN, HTCAPTION, 0
return
#global
onclick gosub *move@GUI
